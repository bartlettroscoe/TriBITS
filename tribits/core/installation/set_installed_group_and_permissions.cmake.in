#!/bin/sh

SET(PROJECT_SET_GROUP_AND_PERMISSIONS_ON_INSTALL_BASE_DIR "@PROJECT_SET_GROUP_AND_PERMISSIONS_ON_INSTALL_BASE_DIR@")
SET(PROJECT_MAKE_INSTALL_GROUP "@PROJECT_MAKE_INSTALL_GROUP@")
SET(PROJECT_MAKE_INSTALL_PERMS_CHANGE "@PROJECT_MAKE_INSTALL_PERMS_CHANGE@")

FUNCTION(ECHO_AND_RUN_CMND)
  STRING(REPLACE ";" " " CMND_STR "${ARGN}")
  MESSAGE(STATUS "Running: ${CMND_STR}")
  EXECUTE_PROCESS(COMMAND ${ARGN} RESULT_VARIABLE RTN_CODE)
  IF (NOT RTN_CODE EQUAL 0)
    MESSAGE(SEND_ERROR "ERROR: Above command failed!")
  ENDIF()
ENDFUNCTION()

IF (EXISTS "${PROJECT_SET_GROUP_AND_PERMISSIONS_ON_INSTALL_BASE_DIR}")

  IF (NOT "${PROJECT_MAKE_INSTALL_GROUP}" STREQUAL "")
    ECHO_AND_RUN_CMND(chgrp ${PROJECT_MAKE_INSTALL_GROUP} -R
      "${PROJECT_SET_GROUP_AND_PERMISSIONS_ON_INSTALL_BASE_DIR}")
  ENDIF()

  IF (NOT "${PROJECT_MAKE_INSTALL_PERMS_CHANGE}" STREQUAL "")
    ECHO_AND_RUN_CMND(chmod ${PROJECT_MAKE_INSTALL_PERMS_CHANGE} -R
      "${PROJECT_SET_GROUP_AND_PERMISSIONS_ON_INSTALL_BASE_DIR}")
  ENDIF()

ELSE()

  MESSAGE(FATAL_ERROR
    ""
    "*** ERROR: The directory:"
    "***"
    "***   ${PROJECT_SET_GROUP_AND_PERMISSIONS_ON_INSTALL_BASE_DIR}"
    "***"
    "*** does not exist so can't fix group and permissions!"
    "***"
    ""
    )

ENDIF()
